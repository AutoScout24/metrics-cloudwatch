group = 'com.blacklocus'
version = '0.4.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.5.0'
    }
}
apply plugin: 'license'

sourceCompatibility = 1.6
targetCompatibility = 1.6

/** Function always returns a new manifest that can be customized */
def defaultManifest() {
    return manifest{
        def git_cmd = "git rev-parse HEAD"
        def git_proc = git_cmd.execute()
        attributes 'SCM-Revision': git_proc.text.trim()
        attributes 'Timestamp': String.valueOf(System.currentTimeMillis())
        attributes 'Build-Host': InetAddress.localHost.hostName
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk:1.6.3'
    compile 'com.codahale.metrics:metrics-core:3.0.1'

    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'com.google.guava:guava:15.0'
    compile 'org.slf4j:slf4j-api:1.7.5'

    testCompile 'junit:junit:4.11'
    testCompile 'org.slf4j:slf4j-simple:1.7.5'
}

jar {
    manifest = defaultManifest()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
    manifest = defaultManifest()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest = defaultManifest()
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

        repository(
                // for snapshots
                url: "https://oss.sonatype.org/content/repositories/snapshots/"
                // for staging/release
//                url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        ) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'metrics-cloudwatch'
            description "A codahale metrics reporter to Amazon CloudWatch"
            url 'https://github.com/blacklocus/metrics-cloudwatch'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'scm:https://github.com/blacklocus/metrics-cloudwatch.git'
                connection 'scm:git://github.com/blacklocus/metrics-cloudwatch.git'
                developerConnection 'scm:git@github.com:blacklocus/metrics-cloudwatch.git'
            }
            organization {
                name 'BlackLocus'
                url 'http://www.blacklocus.com'
            }
            developers {
                developer {
                    id 'dirkraft'
                    name 'Jason Dunkelberger'
                    organization 'BlackLocus'
                    organizationUrl 'http://www.blacklocus.com'
                }
            }
        }
    }
}

idea {
    project {
        languageLevel = '1.6'
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

license {
    header rootProject.file('src/license/HEADER')
}
